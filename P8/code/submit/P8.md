# 模块

## fpga_top

### fpga_top

- 顶层模块，包含系统桥，外设，以及 CPU

## CPU

### PC_Calc

- 计算下一条指令地址
- 输入
	- `[31:0] PC`
	- `[25:0] instr_index`
	- `[15:0] offset`
	- `Beq`
	- `[31:0] RegData`
	- `[1:0] IMControl`
	- `eret` 异常返回
	- `Req` 进入中断处理程序
	- `[31:0] EPCOut` EPC 的值
- 输出
	- `[31:0] nPC`
	- `[4:0] ExcCodeOut`
	
-  对应跳转

	| IMControl |    操作    |
	| :-------: | :--------: |
	|   IM_4    |    PC+4    |
	|   IM_j    | 跳转偏移量 |
	|   IM_jr   | 跳转寄存器 |
	|  IM_Beq   |    分支    |

### GRF

- 寄存器堆
- 输入
	- `clk`
	- `reset`
	- `[4:0] RA1`
	- `[4:0] RA2`
	- `RegWrite`
	- `[4:0] WA`
	- `[31:0] WD`
- 输出
	- `[31:0] RD1`
	- `[31:0] RD2`
- `$0` 寄存器永远为 0
- 同步复位
- 内部转发

### ALU

- 计算单元

- 输入

  - `[31:0] D1`
  - `[31:0] D2`
  - `[15:0] Imm`
  - `[5:0] ALUop`
  - `ALUSrc`
  - `Ext`
  - `[3:0] Instr_type` 指令类型
  - `[4:0] ExcCodeIn`

- 输出

  - `[31:0] out`

  - `[4:0] ExcCodeOut` 异常代码

  	| Instr_type && Behavior |   ExcCode   |
  	| :--------------------: | :---------: |
  	|   type_Load && 溢出    | AdEL : 5'd4 |
  	|   type_Store && 溢出   | AdES : 5'd5 |
  	|   type_CalOv && 溢出   | Ov : 5'd12  |
  	|        defaule         |    5'd0     |

- 功能

	|  ALUop  |
	| :-----: |
	| ALU_add |
	| ALU_sub |
	| ALU_or  |
	| ALU_sll |
	| ALU_lui |

### Mux

- 多路选择器



## Controller

### CP0

- 协处理器
- 输入
	- `clk`
	- `reset`
	- `en` 寄存器写使能信号
	- `[4:0] Addr` 寄存器地址
	- `[31:0] CP0In` CP0 写入数据
	- `[31:0] VPC` 受害 PC
	- `BDIn` 是否为延迟槽指令
	- `[4:0] ExcCodeIn` 异常类型
	- `[5:0] HWIn` 输入中断信号
	- `EXLClr` 复位 EXL
- 输出
  - `[31:0] CP0Out` CP0 读出数据
  - `[31:0] EPCOut` EPC 的值
  - `Req` 进入处理程序请求

### Hazard_Controller

- 冒险控制器
- 输入

  - `clk`
  - `reset`
  - `[4:0] rs_Addr`
  - `[1:0] rs_T_use`
  - `[4:0] rt_Addr`
  - `[1:0] rt_T_use`
  - `[4:0] E_RegWA`
  - `[1:0] E_T_new`
  - `[4:0] M_RegWA`
  - `[1:0] M_T_new`
- 输出

  - `block`
  - `[1:0] D_D1_Sel`
  - `[1:0] D_D2_Sel`
  - `[1:0] E_D1_Sel`
  - `[1:0] E_D2_Sel`
- 

### Controller

- 集中式控制器

- 输入

  - `[31:0] instr`
  - `[4:0] ExcCodeIn`

- 输出

	- `[2:0] IMControl`

	  |                        instr / Class                         |    IMcontrol    |
	  | :----------------------------------------------------------: | :-------------: |
	  | Cal_rr, Cal_ri, Mv_fr, Mv_to, Load, Store, Mul_div, Lui, mfc0, mtc0, syscall |  IM_4 : 3'b000  |
	  |                            Jal, J                            |  IM_j : 3'b001  |
	  |                           Jalr, Jr                           | IM_jr : 3'b010  |
	  |                             beq                              | IM_beq : 3'b011 |
	  |                             bne                              | IM_bne : 3'b100 |
	  |                           default                            |     3'b000      |

	  

	- `[1:0] WA_Sel` 写入数据地址选择信号

	  |          Class          |      WA_sel       |
	  | :---------------------: | :---------------: |
	  |   Cal_rr, Mv_fr, Jalr   | WA_Sel_rd : 2'b00 |
	  | Cal_ri, Load, Lui, mfc0 | WA_Sel_rt : 2'b01 |
	  |           Jal           | WA_Sel_ra : 2'b10 |
	  |         default         |       2'b00       |

	- `[1:0] ALUData_Sel`

	  |          instr / Class           |       ALUData_Sel       |
	  | :------------------------------: | :---------------------: |
	  | Cal_rr, Cal_ri, Load, Store, Lui | ALUData_Sel_out : 2'b00 |
	  |            Jal, Jalr             | ALUData_Sel_pc : 2'b01  |
	  |               mflo               | ALUData_Sel_lo : 2'b10  |
	  |               mfhi               | ALUData_Sel_hi : 2'b11  |

	  

	- `ALUSrc`

	  |        Class        | ALUSrc |
	  | :-----------------: | :----: |
	  | Cal_ri, Load, Store |   1    |
	  |       default       |   0    |

	  

	- `Ext`

	  |      instr / Class       | Ext  |
	  | :----------------------: | :--: |
	  | addi, addiu, Load, Store |  1   |
	  |         default          |  0   |
	
	  
	
	- `[5:0] ALUop` 
	
	  | instr / Class |  ALUop   |
	  | :-----------: | :------: |
	  |    Cal_rr     |  funct   |
	  |     addi      | ALU_addu |
	  |     addiu     | ALU_addu |
	  |     andi      | ALU_and  |
	  |      ori      |  ALU_or  |
	  |  Load, Store  | ALU_addu |
	  |      Lui      | ALU_lui  |
	  |    default    |   6'b0   |
	
	  
	
	- `[3:0] BEop`
	
	  |  instr  |       BEop        |
	  | :-----: | :---------------: |
	  |   lw    |   BE_lw : 4'd1    |
	  |   lb    |   BE_lb : 4'd2    |
	  |   lbu   |   BE_lbu : 4'd3   |
	  |   lh    |   BE_lh : 4'd4    |
	  |   lhu   |   BE_lhu : 4'd5   |
	  |   sw    |   BE_sw : 4'd6    |
	  |   sb    |   BE_sb : 4'd7    |
	  |   sh    |   BE_sh : 4'd8    |
	  | default | BE_default : 4'd0 |
	
	- `[2:0] MDop`
	
	  | instr |       MDop        |
	  | :---: | :---------------: |
	  | mfhi  | MD_mfhi : 3'b000  |
	  | mflo  | MD_mflo : 3'b001  |
	  | mthi  | MD_mthi : 3'b010  |
	  | mtlo  | MD_mtlo : 3'b011  |
	  | mult  | MD_mult : 3'b100  |
	  | multu | MD_multu : 3'b101 |
	  |  div  |  MD_div : 3'b110  |
	  | divu  | MD_divu : 3'b111  |
	
	- `[1:0] RegData_Sel`
	
	  |          Instr / Class           |       RegData_Sel       |
	  | :------------------------------: | :---------------------: |
	  | Cal_rr, Cal_ri, Mv_fr, Jal, Jalr | RegData_Sel_out : 2'b00 |
	  |               Load               | RegData_Sel_mem : 2'b01 |
	  |               mfc0               | RegData_Sel_cp0 : 2'b10 |
	  |             default              |          2'b00          |
	
	- `RegWrite`
	
	  |                       Class                        | RegWrite |
	  | :------------------------------------------------: | :------: |
	  | Cal_rr, Cal_ri , Mv_fr, Load, Lui, Jal, Jalr, mfc0 |    1     |
	  |                      default                       |    0     |
	  
	- `start`
	
	  |  Class  | start |
	  | :-----: | :---: |
	  | Mul_div |   1   |
	  | default |   0   |
	
	
	
	- `[4:0] rs_Addr`
	- `[4:0] rt_Addr`
	- `[2:0] T_new` 
	
		|         Class         | T_new |
		| :-------------------: | :---: |
		| Cal_rr, Cal_ri, Mv_fr |   2   |
		|      Load, mfc0       |   3   |
		|          Lui          |   1   |
		|       Jal, Jalr       |   1   |
		|        default        |   0   |
	
	- `[2:0] rs_T_use`
	
		|                    Class                    | rs_T_use |
		| :-----------------------------------------: | :------: |
		| Cal_rr, Cal_ri, Mv_to, Load, Store, Mul_div |    1     |
		|               Br_r2, Jr, Jalr               |    0     |
		|                   default                   |    5     |
	
	
	
	- `[2:0] rt_T_use`
	
		|      Class      | rt_T_use |
		| :-------------: | :------: |
		| Cal_rr, Mul_div |    1     |
		|      Br_r2      |    0     |
		|   Store, mtc0   |    2     |
		|     default     |    5     |
	
	
	
	- `CP0Write` CP0 写入使能信号
	
		|  Instr  | CP0Write |
		| :-----: | :------: |
		|  mtc0   |    1     |
		| default |    0     |
	
	- `[3:0] Instr_type` 指令类型
	
	  | Instr / Class  |     Instr_type      |
	  | :------------: | :-----------------: |
	  |      Load      |  type_Load : 4'd1   |
	  |     Store      |  type_Store : 4'd2  |
	  | add, sub, addi |   type_Cal : 4'd3   |
	  |      Lui       |   type_Lui : 4'd4   |
	  |   Jal, Jalr    |  type_Link : 4'd5   |
	  |  Mv_fr, Mv_to  |   type_MD : 4'd6    |
	  |      mfc0      |  type_Mfc0 : 4'd7   |
	  |    default     | type_default : 4'd0 |
	
	- `[4:0] ExcCodeOut` 异常类型
	
	  |     Behavior      |   ExcCodeOut   |
	  | :---------------: | :------------: |
	  | ExcCodeIn != 5'd0 |   ExcCodeIn    |
	  |     系统调用      | syscall : 5'd8 |
	  |     未知指令      |   RI : 5'd10   |
	  |      default      |      5'd0      |
	
	- `eret` 异常返回
	
	- `BD` 延迟槽
	
	  |             Class              |  BD  |
	  | :----------------------------: | :--: |
	  | Br_r1, Br_r2, Jal, J, Jalr, Jr |  1   |
	  |            default             |  0   |
	
	  

## 外设

### Bridge

- 系统桥

- 输入

  - `clk`
  - `reset`
  - `[31:0] Addr` 输入地址
  - `[31:0] WriteData` 写入数据
  - `[4:0] ExcCodeIn` BE 模块异常代码
  - `[3:0] Op` 操作类型
  - `Req` 中断请求信号
  - `[31:0] m_data_rdata` 数据存储器读出数据
  - `[31:0] Dout_T0` 计时器 T0 寄存器值
  - `[31:0] dip_switch_3_0, dip_switch_7_4` 微动开关输入，取反
  - `[7:0] user_key` 8 个按钮输入
  - `[31:0] uart_rdata` UART 读出数据

- 输出

  - `[4:0] ExcCodeOut` 异常代码
  - `[31:0] ReadData` 读出数据
  - `[31:0] m_data_addr`  数据存储器地址
  - `[31:0] m_data_wdata` 数据存储器写入数据
  - `[3:0] m_data_byteen` 数据存储器字节使能信号 
  - `[31:2] Addr_T0` 计时器 T0 地址
  - `WE_T0` 计时器 T0 写入使能信号
  - `[31:0] Din_T0` 计时器 T0 写入数据
  - `[3:0] led_byteen` LED 字节使能信号
  - `[31:0] led_wdata` LED 写入数据
  - `tube_addr` 数码管写入地址
  - `[3:0] tube_byteen` 数码管字节使能信号
  - `[31:0] tube_wdata` 数码管写入数据
  - `uart_we` UART 写入信号
  - `uart_load` UART 读出信号 
  - `[1:0] uart_addr` UART 写入地址
  - `[31:0] uart_wdata` UART 写入数据

- `[2:0] devide` 设备编号

  |            Addr            |         device         |
  | :------------------------: | :--------------------: |
  | [0x0000_0000, 0x0000_2FFF] |    device_DM : 3'd1    |
  | [0x0000_7F00, 0x00007F0B]  |    device_T0 : 3'd2    |
  | [0x0000_7F30, 0x00007F3F]  |   device_UART: 3'd3    |
  | [0x0000_7F50, 0x0000_7F57] |   device_TUBE : 3'd4   |
  | [0x0000_7F60, 0x0000_7F67] |   device_DIP : 3'd5    |
  | [0x0000_7F68, 0x0000_7F6b] |    device_KEY: 3'd6    |
  | [0x0000_7F70, 0x0000_7F73] |    device_LED: 3'd7    |
  |          default           | device_undefine : 3'd0 |

  

### TIMER

- 输入
	- `clk`
	- `reset`
	- `[31:2] Addr`
	- `WE`
	- `[31:0] Din` 写入数据
- 输出
	- `IRQ` 中断信号
	- `[31:0] Dout`  读出数据

### IM

- 输入
	- `clk`
	- `[11:0] addra` 地址

- 输出
	- `[31:0] douta` 读出数据

- 特性
	- 同步读，同步写
	- 采用 IP 核生成

### DM

- 输入
	- `clk`
	- `[3:0] wea` 字节使能信号
	- `[11:0] addra` 地址
	- `[31:0] dina` 写入数据
- 输出
	- `[31:0] douta` 读出数据
- 特性
	- 同步读，同步写
	- 采用 IP 核生成

### Digital_tube

- 数码管驱动模块

- 输入
	- `clk`
	- `reset`
	- `[3:0] byteen`
	- `Addr`
	- `[31:0] wdata`
- 输出
	- `[7:0] digital_tube2`
	- `digital_tube_sel2`
	- `7:0] digital_tube1`
	- `[3:0] digital_tube_sel1`
	- `[7:0] digital_tube0`
	- `[3:0] digital_tube_sel0`
- 特性
	- 只写
	- 支持按字节写
	- 显示写入数据所表示的有符号数字

### LED_CTRL

- LED 驱动模块
- 输入
	- `clk`
	- `reset`
	- `[3:0] byteen`
	- `[31:0] wdata`
- 输出
	- `[31:0] led_light`
- 特性
	- 只写
	- 支持按字节写
	- 直接输出写入数据

### UART

- UART 控制器
- 输入
	- `clk`
	- `rstn`
	- `we`
	- `load`
	- `uart_rxd`
	- `[1:0] Addr`
	- `[31:0] wdata`
- 输出
	- `IRQ`
	- `uart_txd`
	- `[31:0] rdata`

- 特性
	- 实现 UART 的四个对应寄存器
	- 将 uart_rx 和 uart_tx 封装于 UART_CTRL 模块中

### UART_CTRL

- 输入
	- `clk`
	- `rstn`
	- `we`
	- `load`
	- `[7:0] wdata`
	- `[15:0] DIVR, DIVT`
	- `uart_rxd`
- 输出
	- `uart_txd`
	- `uart_ready`
	- `uart_busy`

- 特性
	- 内部实现了一个大小为 32 字的缓冲区，便于连续输出多个数据
	- 缓冲区满时 `uart_busy` 置 1
	- 在缓冲区有数据且 uart_tx 模块空闲时输出缓冲区的第一个字
	- 输出 `uart_ready` 信号，UART 模块中当该信号为 1 时输出中断信号
	- 读出数据时 `load` 置 1，表示数据已读出，可以继续接收数据

# 汇编程序

```assembly
.macro uart_write(%data)
	wait:
	lw $t6, 0x7F34($0) # LSR
	andi $t6, $t6, 0x20
	beq $t6, $0, wait
	nop
	sw %data, 0x7F30($0) # DATA
.end_macro

.macro trans(%n)
	ori $t7, $0, 10
	subu $t7, %n, $t7
	slt $t7, $t7, $0
	bne $t7, $0, lt
	nop
ge:
	addi %n, %n, 39
lt:
	addi %n, %n, 48
.end_macro

.macro print(%n)
	lui $t7, 0x8000
	and $t7, %n, $t7
	or $t8, $0, %n
	beq $t7, $0, print_pos
	ori $t9, $0, 0x0
	subu $t8, $0, %n
	ori $t9, $0, 0x1
print_pos:
	beq $s1, $0, print_uart
	nop
print_tube:
	sw %n, 0x7F70($0) # LED
	sw $t8, 0x7F50($0) # tube[0]
	sw $t9, 0x7F54($0) # tube[1]
	j print_end
	nop
print_uart:
	beq $t9, $0, uart_pos
	ori $t5, $0, 45 # '-'
	uart_write($t5)
uart_pos:
	srl $t5, $t8, 28
	andi $t5, $t5, 0xf
	trans($t5)
	uart_write($t5)
	
	srl $t5, $t8, 24
	andi $t5, $t5, 0xf
	trans($t5)
	uart_write($t5)

	srl $t5, $t8, 20
	andi $t5, $t5, 0xf
	trans($t5)
	uart_write($t5)

	srl $t5, $t8, 16
	andi $t5, $t5, 0xf
	trans($t5)
	uart_write($t5)

	srl $t5, $t8, 12
	andi $t5, $t5, 0xf
	trans($t5)
	uart_write($t5)

	srl $t5, $t8, 8
	andi $t5, $t5, 0xf
	trans($t5)
	uart_write($t5)

	srl $t5, $t8, 4
	andi $t5, $t5, 0xf
	trans($t5)
	uart_write($t5)

	andi $t5, $t8, 0xf
	trans($t5)
	uart_write($t5)

print_end:

.end_macro

.text
start:
	ori $a0, $0, 0x2401 
	mtc0 $a0, $12 # 允许中断
	lb $v0, 0x7F68($0) # 按键开关
	andi $s0, $v0, 0x1 # $s0 模式控制
	beq $s0, $0, timer # 
	andi $s1, $v0, 0x2 # $s1 显示控制
calc: # 计算器模式
	andi $s2, $v0, 0x4 # $s2 按键 2
	andi $s3, $v0, 0x8 # $s3 按键 3
	andi $s4, $v0, 0x10 # $s4 按键 4
	andi $s5, $v0, 0x20 # $s5 按键 5
	andi $s6, $v0, 0x40 # $s6 按键 6
	andi $s7, $v0, 0x80 # $s7 按键 7
	lw $a1, 0x7F64($0) # 输入操作数 1
	lw $a2, 0x7F60($0) # 输入操作数 2
	beq $s2, $0, calc_add
	nop
	beq $s3, $0, calc_sub
	nop
	beq $s4, $0, calc_mult
	nop
	beq $s5, $0, calc_div
	nop
	beq $s6, $0, calc_and
	nop
	beq $s7, $0, calc_or
	nop
	j calc_default
	nop

	calc_add:
		addu $a3, $a1, $a2
		j calc_print
		nop
	calc_sub:
		subu $a3, $a1, $a2
		j calc_print
		nop
	calc_mult:
		mult $a1, $a2
		mflo $a3
		j calc_print
		nop
	calc_div:
		div $a1, $a2
		mflo $a3
		j calc_print
		nop
	calc_and:
		and $a3, $a1, $a2
		j calc_print
		nop
	calc_or:
		or $a3, $a1, $a2
		j calc_print
		nop
	calc_default:
		j calc_print
		nop
timer: # 计时器模式
	lw $a0, 0x7F60($0) # 输入初值
	lui $a2, 0x017d 
	ori $a2, $a2, 0x7840 # 计时器初值 25M
	sw $a2, 0x7F04($0) 
	andi $s2, $v0, 0x4 # 计数方式
	beq $s2, $0, down # 
	ori $a1, $0, 0x9 # 计数器控制器

	up: # 向上计数
		ori $a3, $0, 0
		print($a3)
		sw $a1, 0x7F00($0) # 开始计时
		wait_up:
			lw $t0, 0x7F60($0) # 输入初值
			bne $t0, $a0, start # 初值变化时重启
			nop
			bne $a3, $a0, wait_up # 等待计时终止
			nop
			sw $0, 0x7F00($0) # 停止计时
			j end_timer
			nop
	down: # 向下计数
		or $a3, $0, $a0
		print($a3)
		sw $a1, 0x7F00($0) # 开始计时
		wait_down:
			lw $t0, 0x7F60($0) # 输入初值
			bne $t0, $a0, start # 初值变化时重启
			nop
			bne $a3, $0, wait_down # 等待计时终止
			nop
			sw $0, 0x7F00($0) # 停止计时
			j end_timer
			nop

end_timer:
	lw $t0, 0x7F60($0) # 输入初值
	bne $t0, $a0, start # 初值变化时重启
	nop
	lb $t1, 0x7F68($0) # 输入初值
	bne $t1, $v0, start # 初值变化时重启
	nop
	j end_timer
	nop

calc_print:
	print($a3)
end_calc:
	lw $t1, 0x7F64($0) # 输入初值
	bne $t1, $a1, start # 初值变化时重启
	nop
	lw $t1, 0x7F60($0) # 输入初值
	bne $t1, $a2, start # 初值变化时重启
	nop
	lb $t1, 0x7F68($0) # 输入初值
	bne $t1, $v0, start # 初值变化时重启
	nop
	j end_calc
	nop

.ktext 0x4180
_entry:
	jal _save_context
	nop
	mfc0 $k0, $13
	andi $t0, $k0, 0x7C 
	beq $t0, $0, _interrupt
	mfc0 $k1, $14

_exc: # 异常处理
	addiu $k1, $k1, 4
	mtc0 $k1, $14
	j _end
	nop

_interrupt: # 中断处理
	andi $t0, $k0, 0x400
	bne $t0, $0, _timer
	nop
_uart: # uart 回显
	lw $t0, 0x7F30($0)
	uart_write($t0)
	j _end
	nop

_timer: # 处理计时器中断
	beq $s2, $0, _down # 
	ori $t1, $0, 0x1
_up:
	addu $a3, $a3, $t1
	j _next_time
	nop
_down:
	subu $a3, $a3, $t1
_next_time:
	print($a3)
	sw $a1, 0x7F00($0) # 开始计时
	j _end
	nop

_save_context:
	sw $t0, 4($sp)
	sw $t1, 8($sp)
	sw $t2, 12($sp)
	sw $t3, 16($sp)
	sw $t4, 20($sp)
	sw $t5, 24($sp)
	sw $t6, 28($sp)
	sw $t7, 32($sp)
	sw $t8, 36($sp)
	sw $t9, 40($sp)
	jr $ra
	nop
_load_context:
	lw $t0, 4($sp)
	lw $t1, 8($sp)
	lw $t2, 12($sp)
	lw $t3, 16($sp)
	lw $t4, 20($sp)
	lw $t5, 24($sp)
	lw $t6, 28($sp)
	lw $t7, 32($sp)
	lw $t8, 36($sp)
	lw $t9, 40($sp)
	jr $ra
	nop

_end:
	jal _load_context
	nop
	eret
	nop

```

# 问题

### 综合时一些必要寄存器被优化

- 代码中使用的不可综合的语法，且 ISE 没有提示，比如对 x 值进行比较
